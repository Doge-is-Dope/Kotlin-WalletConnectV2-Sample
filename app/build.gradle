plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.example.walletconnectsample'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.walletconnectsample"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def secretPropertiesFile = rootProject.file("secret.properties")
        def secretProperties = new Properties()
        secretProperties.load(new FileInputStream(secretPropertiesFile))
        buildConfigField("String", "PROJECT_ID", secretProperties['WALLETCONNECT_PROJECT_ID'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // WalletConnect
    def wallet_connect_version = "1.1.1"
    implementation(platform("com.walletconnect:android-bom:$wallet_connect_version"))
    // bug: https://github.com/WalletConnect/WalletConnectKotlinV2/issues/495
    implementation("com.walletconnect:android-core") {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    // bug: https://github.com/WalletConnect/WalletConnectKotlinV2/issues/495
    implementation("com.walletconnect:sign") {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation("com.walletconnect:web3wallet:1.0.0") // android-bom 1.2.0 will include wallet

    // Lifecycle
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // CameraX
    def camerax_version = "1.2.0-rc01"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation 'androidx.camera:camera-mlkit-vision:1.2.0-beta02'
    // MLKit for QR Code
    implementation 'com.google.mlkit:barcode-scanning:17.0.3'

    // Navigation components
    def nav_version = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Image loader
    implementation "io.coil-kt:coil:2.2.2"

    // Logger
    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}